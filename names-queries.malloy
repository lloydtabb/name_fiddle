import "names.malloy"

// --
// Name: Preview the table
query: names->{ 
  project: *
  limit: 100
}

// --
// Name: 100 Most Popular Names
query: names->by_name + {limit: 100}

// --
// Name: 100 Most Popular Male Names
query: names->by_name + {
  where: gender = 'M'
  limit: 100
}

// --
// Name: 100 Most Popular Female Names since 2010
  query: names->by_name {
    where: gender = 'F' and `year` >= 2010
    limit: 100
  }

// --
// Name: Popular 'J' names in each state since 2010
query: names-> by_state + {
  where: decade = 2010
  where: name ~ r'^J'
  nest: male_names is by_name + {
    where: gender = 'M'
    limit: 10
  }
  nest: female_names is by_name + {
    where: gender = 'F'
    limit: 10
  }
  limit: 50
}

// --
// Name: Most Iconic names in NY.
query: names-> {
  group_by: name
  aggregate: 
    percent_ny is population {? state='NY'} / population
  having: population > 3000
}

// --
// Name: Most Iconic names per State.
query: names-> {
  group_by: state
  aggregate: population
  nest: names_list_detail is {
    group_by: name
    aggregate: 
      percent_in_state is population / exclude(population, state)
    limit: 40
    having: population > 1000
  }
  limit: 50
}

// // --
// // Name: 100 Most Popular Names
//   query: name_dashboard is by_name {
//     nest:
//       by_decade
//       by_state
//     limit: 10
//   }
// }

// // named querys
// //  see: https://looker-open-source.github.io/malloy/documentation/language/query.html
// query: j_names is names -> name_dashboard {? name ~ r'J'}
